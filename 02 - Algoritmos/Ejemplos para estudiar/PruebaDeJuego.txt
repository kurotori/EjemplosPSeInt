Funcion dibujarTablero(limiteX,limiteY,posX,posY,mapa)
	Definir contX, contY Como Entero;
	Para contY=-1 Hasta limiteY Con Paso 1 Hacer
		Escribir Sin Saltar "*";
		Para contX=-1 Hasta limiteX Con Paso 1 Hacer
			Si contX=-1 o contX=limiteX Entonces
				Escribir Sin Saltar "";
			SiNo
				Si contY=-1 o contY=limiteY Entonces
					Escribir Sin Saltar "*";
				SiNo
					DibujarContenidoDeMapa(posX,posY,contX,contY,mapa);//Escribir Sin Saltar " ";
				FinSi
			FinSi
			
		Fin Para
		Escribir "*";
	Fin Para
FinFuncion

Funcion DibujarContenidoDeMapa(posX,posY,coordX,coordY,mapa)
	Si coordX=posX y coordY = posY Entonces
		Escribir Sin Saltar "@";
	SiNo
		Segun mapa(coordX,coordY) Hacer
			0:
				Escribir Sin Saltar " ";
			2:
				Escribir Sin Saltar " ";
			3:
				Escribir Sin Saltar " ";
			4:
				Escribir Sin Saltar " ";
			5:
				Escribir Sin Saltar "Y";
			6:
				Escribir Sin Saltar "t";
			De Otro Modo:
				Escribir Sin Saltar " ";
		FinSegun
	FinSi
FinFuncion

Funcion RellenarMapa(mapaVacio,limiteX,limiteY)
	Definir contX,contY Como Entero;
	Para contY=0 Hasta limiteY-1 Con Paso 1 Hacer
		Para contX=0 Hasta limiteX-1 Con Paso 1 Hacer
			mapaVacio(contX,contY) = Aleatorio(0,10);
		Fin Para
	Fin Para
FinFuncion

Funcion resultado<-ChequearObstaculo(posX,posY,mapa)
	Definir resultado Como Logico;
	resultado=Falso;
	Segun mapa(posX,posY) Hacer
		0:
			resultado = Verdadero;
		1:
			resultado = Verdadero;
		7:
			resultado = Verdadero;
		8:
			resultado = Verdadero;
		9:
			resultado = Verdadero;
		10:
			resultado = Verdadero;
		De Otro Modo:
			Escribir mapa(posX,posY);
			Esperar Tecla;
	FinSegun
FinFuncion

Funcion pos<-ChequearPos(posActual, limite)
	Definir pos Como Entero;
	Si posActual < 0 Entonces
		pos = 0;
	SiNo
		Si posActual > limite-1
			pos = limite-1;
		SiNo
			pos = posActual;
		FinSi
	Fin Si
FinFuncion

Funcion nuevaPosY<-MoverArriba(posYActual, limiteY)
	Definir nuevaPosY Como Entero;
	nuevaPosY = ChequearPos(posYActual - 1, limiteY);
FinFuncion

Funcion nuevaPosY<-MoverAbajo(posYActual, limiteY)
	Definir nuevaPosY Como Entero;
	nuevaPosY = ChequearPos( posYActual + 1, limiteY);
FinFuncion

Funcion nuevaPosX<-MoverDerecha(posXActual, limiteX)
	Definir nuevaPosX Como Entero;
	nuevaPosX = ChequearPos( posXActual + 1, limiteX);
FinFuncion

Funcion nuevaPosX<-MoverIzquierda(posXActual,limiteX)
	Definir nuevaPosX Como Entero;
	nuevaPosX = ChequearPos( posXActual - 1, limiteX);
FinFuncion


//Juego

Algoritmo PruebaDeJuego
	Definir posX, posY, limiteX, limiteY Como Entero;
	Definir posXnueva, posYnueva Como Entero;
	posXnueva=0;
	posYnueva=0;
	posX=0;
	posY=0;
	limiteX=85;
	limiteY=18;
	
	Definir mapa Como Entero;
	Dimension mapa(limiteX,limiteY);
	
	RellenarMapa(mapa,limiteX,limiteY);
	
	Definir opcionU Como Caracter;
	opcionU="";
	
	Mientras opcionU<>"salir" Hacer
		Escribir posX,",",posY;
		dibujarTablero(limiteX, limiteY,posX,posY,mapa);
		Leer opcionU;
		opcionU=Minusculas(opcionU);
		
		Segun opcionU Hacer
			"s":
				posYnueva=MoverAbajo(posY,limiteY);
				Si ChequearObstaculo(posX,posYnueva,mapa) Entonces
					posY = posYnueva;
				FinSi
			"w":
				posY=MoverArriba(posY,limiteY);
			"a":
				posX=MoverIzquierda(posX,limiteX);
			"d":
				posX=MoverDerecha(posX,limiteX);
			De Otro Modo:
				posX=posX;
				posY=posY;
		Fin Segun
		
		
		Limpiar Pantalla;
	Fin Mientras
	
	
FinAlgoritmo
